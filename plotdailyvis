#!/bin/csh
#
# Script for creating and plotting a collection of daily visibility files using gnuplot
#
# Input are all Avis3 visibility (*_avis.txt) files present in the current directory
# Avis3_config.txt file in the current directory is read for obtaining start time and span
#
# Dipankar Bhattacharya Sep 2016
#

set cfgdir = '.' ;
set visdir = '.' ;

set tstart = `grep StartDate $cfgdir/Avis3_config.txt | awk '{print $2 "/" $3 "/" $4}'` ;
set tspan = `grep SpanDays $cfgdir/Avis3_config.txt | awk '{print $2}'` ;

if (-f dailyvis.gnu) then
   rm -f dailyvis.gnu ;
endif

#
# create temporary gnuplot script
#
echo set term post port color solid \"Helvetica\" 10 > dailyvis.gnu ;
echo flist=system\(\"echo \$vflist\"\) >> dailyvis.gnu ;
echo set multiplot layout 4,2  >> dailyvis.gnu ;
echo do for \[visfilename in flist\]\{  >> dailyvis.gnu ;
echo set title visfilename  >> dailyvis.gnu ;
echo set xlabel \"Days from  $tstart\"  >> dailyvis.gnu ;
echo set ylabel \"Visibility \(ks/day\)\"  >> dailyvis.gnu ;
echo set size 0.5,0.25  >> dailyvis.gnu ;
echo set xrange \[0:$tspan\]  >> dailyvis.gnu ;
echo set yrange \[0:80\]  >> dailyvis.gnu ;
echo set label 1 \"LXP/CZT\" at graph 0.1,0.9 left font \"Helvetica,6\" tc rgbcolor \"red\" >> dailyvis.gnu ;
echo set label 2 \"SXT\" at graph 0.5,0.9 center font \"Helvetica,6\" tc rgbcolor \"blue\" >> dailyvis.gnu ;
echo set label 3 \"UVIT\" at graph 0.9,0.9 right font \"Helvetica,6\" tc rgbcolor \"magenta\" >> dailyvis.gnu ;
echo plot visfilename u 1:2 t\"\" w l lc rgbcolor \"red\" lw 2, visfilename u 1:3 t\"\" w l lc rgbcolor \"blue\" lw 2, visfilename u 1:4 t\"\" w l lc rgbcolor \"magenta\" lw 2  >> dailyvis.gnu ;
echo \}  >> dailyvis.gnu ;

if (-f dailyvis.awk) then
   rm -f dailyvis.awk ;
endif

#
# create temporary awk file needed to compute daily visibility from orbit-wise visibility
#
echo BEGIN\{ dt0=0.0 \; vsumt=0.0 \; vsumn=0.0 \; vsumu=0.0\; \} > dailyvis.awk ;
echo \{  >> dailyvis.awk ;
echo    dt = \$2+0.0 \;  >> dailyvis.awk ;
echo    if \(\(dt-dt0\) \< 1.0\)  >> dailyvis.awk ;
echo    \{  >> dailyvis.awk ;
echo       vsumt=vsumt+\$7 \;  >> dailyvis.awk ;
echo       vsumn=vsumn+\$8 \;  >> dailyvis.awk ;
echo       vsumu=vsumu+\$9 \;  >> dailyvis.awk ;
echo    \}  >> dailyvis.awk ;
echo    else  >> dailyvis.awk ;
echo    \{  >> dailyvis.awk ;
echo       vsumt=vsumt/1000.0 \;  >> dailyvis.awk ;
echo       vsumn=vsumn/1000.0 \;  >> dailyvis.awk ;
echo       vsumu=vsumu/1000.0 \;  >> dailyvis.awk ;
echo       dt0=dt0+1.0 \;  >> dailyvis.awk ;
echo       printf \"\%6.1f \%5.2f \%5.2f \%5.2f\\n\", dt0, vsumt, vsumn, vsumu \;  >> dailyvis.awk ;
echo       vsumt=0.0 \;  >> dailyvis.awk ;
echo       vsumn=0.0 \;  >> dailyvis.awk ;
echo       vsumu=0.0 \;  >> dailyvis.awk ;
echo    \}  >> dailyvis.awk ;
echo \}  >> dailyvis.awk ;

#
# execute the awk file on each visibility file to create 
# temporary daily visibility (.dvis) files
#
set vislist = `ls -1 $visdir/*_avis.txt` ;
foreach file ($vislist)
    set dvfile = `echo $file | sed "s/_avis.txt/.dvis/g"`;
    grep -v \# $file | awk -f dailyvis.awk > $dvfile ;
end
set outdir = "MP" ;

if (-d $outdir) then
   rm -rf $outdir ;
endif
mkdir $outdir ;

#
# plot the daily visibility files, 8 per page
# place PS file of each page in subdirectory MP
#
@ nvis = `ls -1 *.dvis | wc -l`;
@ nbunch = $nvis / 8;
@ nres = $nvis - 8 * $nbunch;
@ i = 1;
while ($i <= $nbunch)
    @ nhead = $i * 8 ;
    setenv vflist `ls -1 *.dvis | head -$nhead | tail -8` ;
    if ($i < 10) then
       set outpsfile = $outdir/out_00$i.ps ;
    else if ($i < 100) then
       set outpsfile = $outdir/out_0$i.ps ;
    else
       set outpsfile = $outdir/out_$i.ps ;
    endif
    gnuplot dailyvis.gnu > $outpsfile ;
    @ i++ ;
end

if ($nres > 0) then
    setenv vflist `ls -1 *.dvis | head -$nvis | tail -$nres` ;
    if ($i < 10) then
       set outpsfile = $outdir/out_00$i.ps ;
    else if ($i < 100) then
       set outpsfile = $outdir/out_0$i.ps ;
    else
       set outpsfile = $outdir/out_$i.ps ;
    endif
    gnuplot dailyvis.gnu > $outpsfile ;
endif

if ( -f dailyvis.ps ) then
   mv dailyvis.ps dailyvis.ps.old ;
endif

#
# Concatenate all the pages into a single file dailyvis.ps
# in the current directory
#
cat $outdir/*.ps > ./dailyvis.ps ;

#
# Clean up
#
rm -rf $outdir ;
rm *.dvis ;
rm dailyvis.awk ;
rm dailyvis.gnu ;
